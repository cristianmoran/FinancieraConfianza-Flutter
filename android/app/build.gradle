def localProperties = new Properties()
def localPropertiesFile = rootProject.file('local.properties')
if (localPropertiesFile.exists()) {
    localPropertiesFile.withReader('UTF-8') { reader ->
        localProperties.load(reader)
    }
}

def flutterRoot = localProperties.getProperty('flutter.sdk')
if (flutterRoot == null) {
    throw new GradleException("Flutter SDK not found. Define location with flutter.sdk in the local.properties file.")
}

def flutterVersionCode = '1'
def flutterVersionName = '1.0.0'

def dartEnvironmentVariables = [DEFINECONFIANZA_APP_SUFFIX: "dev", DEFINECONFIANZA_APP_NAME: "DEVFinancieraConfianza"];

if (project.hasProperty('dart-defines')) {
    dartEnvironmentVariables = dartEnvironmentVariables + project.property('dart-defines')
            .split(',')
            .collectEntries { entry ->
                def pair = new String(entry.decodeBase64(), 'UTF-8').split('=')
                [(pair.first()): pair.last()]
            }
}
apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply from: "$flutterRoot/packages/flutter_tools/gradle/flutter.gradle"

def keystoreProperties = new Properties()
def keystorePropertiesFile = rootProject.file('key.properties')
if (keystorePropertiesFile.exists()) {
    keystoreProperties.load(new FileInputStream(keystorePropertiesFile))
}

android {
    compileSdkVersion 32

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = '1.8'
    }

    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
    }

    lintOptions {
        disable 'InvalidPackage'
        checkReleaseBuilds false
    }

    defaultConfig {
        // TODO: Specify your own unique Application ID (https://developer.android.com/studio/build/application-id.html).
        applicationId "com.financieraconfianza"
        minSdkVersion 21
        targetSdkVersion 32
        multiDexEnabled true
        versionCode flutterVersionCode.toInteger()
        versionName flutterVersionName
        resValue "string", "app_name", "${dartEnvironmentVariables.DEFINECONFIANZA_APP_NAME}"
    }

    signingConfigs {
        release {
            keyAlias keystoreProperties['keyAlias']
            keyPassword keystoreProperties['keyPassword']
            storeFile keystoreProperties['storeFile'] ? file(keystoreProperties['storeFile']) : null
            storePassword keystoreProperties['storePassword']
        }
    }

    applicationVariants.all { variant ->
        variant.outputs.all {
            def buildType = variant.buildType.name.toUpperCase()
            def date = new Date().format('yyyyMMdd')
            def versionName = variant.versionName
            def versionCode = variant.versionCode
            outputFileName = "${dartEnvironmentVariables.DEFINECONFIANZA_APP_NAME}-${buildType}-${date}-${versionName}-(${versionCode}).apk"
        }
    }

    flavorDimensions "environment"
    productFlavors {

        dev {
            dimension "environment"
            applicationIdSuffix ".${dartEnvironmentVariables.DEFINECONFIANZA_APP_SUFFIX}"
            resValue "string","key_google_maps",""
        }

        prod {
            dimension "environment"
            resValue "string","key_google_maps",""
        }
    }
}

flutter {
    source '../..'
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
}
